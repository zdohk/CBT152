00001  IDENTIFICATION DIVISION.                                         05/05/78
00002  PROGRAM-ID. TIMEHIST.                                            TIMEHIST
00003  ENVIRONMENT DIVISION.                                               LV005
00004  CONFIGURATION SECTION.                                           TIMEHIST
00005  SOURCE-COMPUTER. IBM-370.                                        TIMEHIST
00006  OBJECT-COMPUTER. IBM-370.                                        TIMEHIST
00007  SPECIAL-NAMES.                                                   TIMEHIST
00008      C01 IS TOP-OF-PAGE.                                          TIMEHIST
00009  INPUT-OUTPUT SECTION.                                            TIMEHIST
00010  FILE-CONTROL.                                                    TIMEHIST
00011      SELECT UCRI-CARD    ASSIGN TO UT-S-SYSIN.                    TIMEHIST
00012      SELECT UPRO-PRINTER ASSIGN TO UT-S-PRINTER.                  TIMEHIST
00013      SELECT UDAI-DATA    ASSIGN TO UT-S-TIMEDATA.                 TIMEHIST
00014  DATA DIVISION.                                                   TIMEHIST
00015  FILE SECTION.                                                    TIMEHIST
00016  FD  UCRI-CARD                                                    TIMEHIST
00017      BLOCK CONTAINS 0 RECORDS                                     TIMEHIST
00018      RECORD CONTAINS 80 CHARACTERS                                TIMEHIST
00019      LABEL RECORDS ARE OMITTED.                                   TIMEHIST
00020  01  UCRI-REC.                                                    TIMEHIST
00021  05  UCRI-START        PIC X(6).                                  TIMEHIST
00022  05  UCRI-ILENX.                                                  TIMEHIST
00023  10  UCRI-ILEN         PIC 9(4).                                  TIMEHIST
00024  05     FILLER         PIC X(30).                                 TIMEHIST
00025  05  UCRI-COMMENT      PIC X(40).                                 TIMEHIST
00026  FD  UPRO-PRINTER                                                 TIMEHIST
00027      BLOCK CONTAINS 0 RECORDS                                     TIMEHIST
00028      RECORD CONTAINS 133 CHARACTERS                               TIMEHIST
00029      LABEL RECORDS ARE OMITTED.                                   TIMEHIST
00030  01  UPRO-REC.                                                    TIMEHIST
00031  05     FILLER         PIC X.                                     TIMEHIST
00032  05  UPRO-LINE         PIC X(132).                                TIMEHIST
00033  FD  UDAI-DATA                                                    TIMEHIST
00034      BLOCK CONTAINS 0 RECORDS                                     TIMEHIST
00035      RECORD CONTAINS 4 CHARACTERS                                 TIMEHIST
00036      LABEL RECORDS ARE STANDARD.                                  TIMEHIST
00037  01  UDAI-REC.                                                    TIMEHIST
00038  05  UDAI-ADDR         PIC S9(8) COMP SYNC.                       TIMEHIST
00039  WORKING-STORAGE SECTION.                                         TIMEHIST
00040  01     FILLER.                                                   TIMEHIST
00041  05     FILLER         PIC X(36) VALUE                            TIMEHIST
00042        'TIMEHIST WORKING-STORAGE STARTS HERE'.                    TIMEHIST
00043  05  WS-CARDS-EOF-FLAG PIC X VALUE SPACES.                        TIMEHIST
00044  88  WS-GOT-A-CARD     VALUE SPACES.                              TIMEHIST
00045  88  WS-CARDS-AT-EOF   VALUE 'E'.                                 TIMEHIST
00046 *                                                                 TIMEHIST
00047  05  WS-VALID-FLAG     PIC X VALUE SPACES.                        TIMEHIST
00048  88  WS-INVALID        VALUE 'I'.                                 TIMEHIST
00049  88  WS-VALID          VALUE 'V'.                                 TIMEHIST
00050 *                                                                 TIMEHIST
00051  05  WS-START          PIC S9(8) COMP SYNC.                       TIMEHIST
00052  05  WS-ILEN           PIC S9(4) COMP SYNC.                       TIMEHIST
00053 *                                                                 TIMEHIST
00054  05  WS-UN-HEX         PIC X(6).                                  TIMEHIST
00055  05     FILLER         REDEFINES WS-UN-HEX.                       TIMEHIST
00056  10  WS-UN-HEX-CH      OCCURS 6 TIMES                             TIMEHIST
00057                        INDEXED BY WS-UH-IX                        TIMEHIST
00058                        PIC X.                                     TIMEHIST
00059  05  WS-CHAR.                                                     TIMEHIST
00060  10  WS-CHARU          PIC 9.                                     TIMEHIST
00061 *                                                                 TIMEHIST
00062  05  WS-TITLE.                                                    TIMEHIST
00063  10     FILLER         PIC X(10) VALUE SPACES.                    TIMEHIST
00064  10     FILLER         PIC X(8) VALUE 'TIMEHIST'.                 TIMEHIST
00065  10     FILLER         PIC X(13) VALUE '    INTERVAL='.              CL**4
00066  10  WS-TITLE-INT      PIC ZZZ9.                                     CL**4
00067  10     FILLER         PIC X(11) VALUE SPACES.                       CL**4
00068  10  WS-COMMENT        PIC X(40).                                 TIMEHIST
00069  10     FILLER         PIC X(05) VALUE 'MAX ='.                      CL**4
00070  10  WS-TITLE-MAX      PIC ZZ,ZZZ,ZZ9.                               CL**4
00071  10     FILLER         PIC X(10) VALUE ', TOTAL = '.                 CL**4
00072  10  WS-TITLE-TOTAL    PIC ZZ,ZZZ,ZZ9.                               CL**4
00073 *                                                                    CL**4
00074  05  WS-TOTAL          PIC S9(8) COMP SYNC.                          CL**4
00075 *                                                                 TIMEHIST
00076  05  WS-HIGH           PIC S9(8) COMP SYNC.                       TIMEHIST
00077  05  WS-LOW-COUNT      PIC S9(8) COMP SYNC.                       TIMEHIST
00078  05  WS-HIGH-COUNT     PIC S9(8) COMP SYNC.                       TIMEHIST
00079 *                                                                 TIMEHIST
00080  05  WS-WORK-COUNT     PIC S9(8) COMP SYNC.                       TIMEHIST
00081  05     FILLER         REDEFINES WS-WORK-COUNT.                   TIMEHIST
00082  10  WS-WORK-1B        PIC X.                                     TIMEHIST
00083  10  WS-WORK-3B        PIC XXX.                                   TIMEHIST
00084 *                                                                 TIMEHIST
00085  05  WS-MAX            PIC S9(8) COMP SYNC.                       TIMEHIST
00086 *                                                                 TIMEHIST
00087  05  WS-PRINT-FLAG     PIC X VALUE SPACES.                        TIMEHIST
00088  88  WS-MORE-TO-PRINT  VALUE SPACES.                              TIMEHIST
00089  88  WS-DONE-PRINTING  VALUE 'D'.                                 TIMEHIST
00090 *                                                                 TIMEHIST
00091  05  WS-WORK-AST       PIC S9(4) COMP SYNC.                       TIMEHIST
00092 *                                                                 TIMEHIST
00093  05  WS-PRINT-LINE.                                               TIMEHIST
00094  10  WS-PL-1ST-HALF    PIC X(66).                                 TIMEHIST
00095  10  WS-PL-2ND-HALF    PIC X(66).                                 TIMEHIST
00096 *                                                                 TIMEHIST
00097  05  WS-HALF.                                                     TIMEHIST
00098  10  WS-HALF-FROM      PIC X(6).                                  TIMEHIST
00099  10     FILLER         PIC X VALUE '-'.                           TIMEHIST
00100  10  WS-HALF-TO        PIC X(6).                                  TIMEHIST
00101  10  WS-HALF-TIMES     PIC Z(8).                                  TIMEHIST
00102  10  WS-HALF-AST-F.                                               TIMEHIST
00103  15  WS-HA-CH          OCCURS 45 TIMES                            TIMEHIST
00104                        INDEXED BY WS-HA-IX                        TIMEHIST
00105                        PIC X.                                     TIMEHIST
00106 *                                                                 TIMEHIST
00107  05  WS-LG-FLAG        PIC X VALUE SPACES.                        TIMEHIST
00108  88  WS-LESS           VALUE 'L'.                                 TIMEHIST
00109  88  WS-GTR            VALUE 'G'.                                 TIMEHIST
00110 *                                                                 TIMEHIST
00111  05  WS-REM            PIC S9(4) COMP SYNC.                       TIMEHIST
00112 *                                                                 TIMEHIST
00113  05  WS-HEX-TBL        PIC X(16) VALUE '0123456789ABCDEF'.        TIMEHIST
00114  05     FILLER         REDEFINES WS-HEX-TBL.                      TIMEHIST
00115  10  WS-HEX-CHAR       OCCURS 16 TIMES                            TIMEHIST
00116                        PIC X.                                     TIMEHIST
00117 *                                                                 TIMEHIST
00118  05  WS-HEX-ADDR-F.                                               TIMEHIST
00119  10  WS-HEX-ADDR-CH    OCCURS 6 TIMES                             TIMEHIST
00120                        INDEXED BY WS-HAC-IX                       TIMEHIST
00121                        PIC X.                                     TIMEHIST
00122 *                                                                 TIMEHIST
00123  05  WS-CAPT.                                                     TIMEHIST
00124  10     FILLER         PIC X(21) VALUE '  FROM-    TO   TIMES'.   TIMEHIST
00125  10     FILLER         PIC X(45) VALUE SPACES.                    TIMEHIST
00126  10     FILLER         PIC X(21) VALUE '  FROM-    TO   TIMES'.   TIMEHIST
00127 *                                                                 TIMEHIST
00128  05  WS-PRINT-TABLE.                                              TIMEHIST
00129  10  WS-PRINT-ENTRY    OCCURS 100 TIMES                           TIMEHIST
00130                        INDEXED BY WS-PT-IX.                       TIMEHIST
00131  15  WS-FROMX.                                                    TIMEHIST
00132  20  WS-FROM           PIC S9(8) COMP SYNC.                       TIMEHIST
00133  15  WS-TIMES          PIC S9(8) COMP SYNC.                       TIMEHIST
00134  15  WS-AST-CNT        PIC S9(4) COMP SYNC.                       TIMEHIST
00135  01  WS-TIME-TABLE.                                               TIMEHIST
00136  05  WS-TIME-COUNT     OCCURS 32767 TIMES                         TIMEHIST
00137                        INDEXED BY WS-TC-IX                        TIMEHIST
00138                        PIC X(3).                                  TIMEHIST
00139  05     FILLER         PIC X(32760).                              TIMEHIST
00140  05     FILLER         PIC X(9).                                  TIMEHIST
00141  PROCEDURE DIVISION.                                              TIMEHIST
00142  200-START.                                                       TIMEHIST
00143      OPEN INPUT UCRI-CARD.                                        TIMEHIST
00144      OPEN OUTPUT UPRO-PRINTER.                                    TIMEHIST
00145      PERFORM 210-PROCESS THRU 210-P-EXIT                          TIMEHIST
00146         UNTIL WS-CARDS-AT-EOF.                                    TIMEHIST
00147      CLOSE UPRO-PRINTER.                                          TIMEHIST
00148      CLOSE UCRI-CARD.                                             TIMEHIST
00149      STOP RUN.                                                    TIMEHIST
00150 *                                                                 TIMEHIST
00151 *                                                                 TIMEHIST
00152  210-PROCESS.                                                     TIMEHIST
00153      MOVE SPACES TO WS-VALID-FLAG.                                TIMEHIST
00154      PERFORM 220-AUDIT-CARD THRU 220-AC-EXIT                      TIMEHIST
00155         UNTIL WS-VALID OR WS-CARDS-AT-EOF.                        TIMEHIST
00156      IF WS-GOT-A-CARD                                             TIMEHIST
00157         PERFORM 300-FILE-PROCESS THRU 300-FP-EXIT.                TIMEHIST
00158 *                                                                 TIMEHIST
00159 *                                                                 TIMEHIST
00160  210-P-EXIT.                                                      TIMEHIST
00161      EXIT.                                                        TIMEHIST
00162 *                                                                 TIMEHIST
00163 *                                                                 TIMEHIST
00164  220-AUDIT-CARD.                                                  TIMEHIST
00165      READ UCRI-CARD AT END                                        TIMEHIST
00166         MOVE 'E' TO WS-CARDS-EOF-FLAG                             TIMEHIST
00167         GO TO 220-AC-EXIT.                                        TIMEHIST
00168      MOVE UCRI-REC TO UPRO-LINE.                                  TIMEHIST
00169      WRITE UPRO-REC AFTER ADVANCING TOP-OF-PAGE.                  TIMEHIST
00170      MOVE ZERO TO WS-START.                                       TIMEHIST
00171      IF UCRI-START IS NOT EQUAL TO SPACES                         TIMEHIST
00172         MOVE UCRI-START TO WS-UN-HEX                              TIMEHIST
00173         SET WS-UH-IX TO 1                                         TIMEHIST
00174         PERFORM 230-HEX-TO-BIN THRU 230-HTB-EXIT 6 TIMES.         TIMEHIST
00175      IF WS-INVALID                                                TIMEHIST
00176         MOVE 'INVALID START FIELD' TO UPRO-LINE                   TIMEHIST
00177         WRITE UPRO-REC AFTER ADVANCING 2 LINES.                   TIMEHIST
00178      EXAMINE UCRI-ILENX REPLACING LEADING SPACES BY ZERO.         TIMEHIST
00179      IF UCRI-ILEN IS NOT NUMERIC                                  TIMEHIST
00180         OR UCRI-ILEN IS EQUAL TO ZERO                             TIMEHIST
00181         MOVE 'INVALID INTERVAL LENGTH' TO UPRO-LINE               TIMEHIST
00182         WRITE UPRO-REC AFTER ADVANCING 2 LINES                    TIMEHIST
00183         MOVE 'I' TO WS-VALID-FLAG                                 TIMEHIST
00184      ELSE                                                         TIMEHIST
00185         MOVE UCRI-ILEN TO WS-TITLE-INT                               CL**4
00186         MOVE UCRI-ILEN TO WS-ILEN.                                TIMEHIST
00187      IF NOT WS-INVALID                                            TIMEHIST
00188         MOVE 'V' TO WS-VALID-FLAG                                 TIMEHIST
00189      ELSE                                                         TIMEHIST
00190         MOVE SPACES TO WS-VALID-FLAG.                             TIMEHIST
00191      MOVE UCRI-COMMENT TO WS-COMMENT.                             TIMEHIST
00192 *                                                                 TIMEHIST
00193 *                                                                 TIMEHIST
00194  220-AC-EXIT.                                                     TIMEHIST
00195      EXIT.                                                        TIMEHIST
00196 *                                                                 TIMEHIST
00197 *                                                                 TIMEHIST
00198  230-HEX-TO-BIN.                                                  TIMEHIST
00199      MOVE WS-UN-HEX-CH (WS-UH-IX) TO WS-CHAR.                     TIMEHIST
00200      MULTIPLY 16 BY WS-START.                                     TIMEHIST
00201      IF WS-CHAR IS NUMERIC                                        TIMEHIST
00202         ADD WS-CHARU TO WS-START                                  TIMEHIST
00203      ELSE                                                         TIMEHIST
00204         IF WS-CHAR IS EQUAL TO 'A'                                TIMEHIST
00205            ADD 10 TO WS-START                                     TIMEHIST
00206      ELSE                                                         TIMEHIST
00207         IF WS-CHAR IS EQUAL TO 'B'                                TIMEHIST
00208            ADD 11 TO WS-START                                     TIMEHIST
00209      ELSE                                                         TIMEHIST
00210         IF WS-CHAR IS EQUAL TO 'C'                                TIMEHIST
00211            ADD 12 TO WS-START                                     TIMEHIST
00212      ELSE                                                         TIMEHIST
00213         IF WS-CHAR IS EQUAL TO 'D'                                TIMEHIST
00214            ADD 13 TO WS-START                                     TIMEHIST
00215      ELSE                                                         TIMEHIST
00216         IF WS-CHAR IS EQUAL TO 'E'                                TIMEHIST
00217            ADD 14 TO WS-START                                     TIMEHIST
00218      ELSE                                                         TIMEHIST
00219         IF WS-CHAR IS EQUAL TO 'F'                                TIMEHIST
00220            ADD 15 TO WS-START                                     TIMEHIST
00221      ELSE                                                         TIMEHIST
00222         MOVE 'I' TO WS-VALID-FLAG.                                TIMEHIST
00223      SET WS-UH-IX UP BY 1.                                        TIMEHIST
00224 *                                                                 TIMEHIST
00225 *                                                                 TIMEHIST
00226  230-HTB-EXIT.                                                    TIMEHIST
00227      EXIT.                                                        TIMEHIST
00228 *                                                                 TIMEHIST
00229 *                                                                 TIMEHIST
00230  300-FILE-PROCESS.                                                TIMEHIST
00231      OPEN INPUT UDAI-DATA.                                        TIMEHIST
00232      MOVE LOW-VALUES TO WS-TIME-TABLE.                            TIMEHIST
00233      MOVE WS-ILEN TO WS-HIGH.                                     TIMEHIST
00234      MULTIPLY 43690 BY WS-HIGH.                                   TIMEHIST
00235      SUBTRACT 1 FROM WS-HIGH.                                     TIMEHIST
00236      ADD WS-START TO WS-HIGH.                                     TIMEHIST
00237      MOVE ZERO TO WS-LOW-COUNT, WS-HIGH-COUNT.                    TIMEHIST
00238      MOVE ZERO TO WS-TOTAL.                                          CL**4
00239      PERFORM 310-INPUT-DATA THRU 310-ID-EXIT.                     TIMEHIST
00240      CLOSE UDAI-DATA.                                             TIMEHIST
00241      MOVE LOW-VALUES TO WS-WORK-1B.                               TIMEHIST
00242      MOVE ZERO TO WS-MAX.                                         TIMEHIST
00243      SET WS-TC-IX TO 1.                                           TIMEHIST
00244      PERFORM 320-MAX-GET THRU 320-MG-EXIT.                        TIMEHIST
00245      MOVE SPACES TO WS-PRINT-FLAG.                                TIMEHIST
00246      MOVE WS-MAX TO WS-TITLE-MAX.                                    CL**4
00247      MOVE WS-TOTAL TO WS-TITLE-TOTAL.                                CL**4
00248      SET WS-TC-IX TO 1.                                           TIMEHIST
00249      PERFORM 330-PRINT-ALL THRU 330-PA-EXIT                       TIMEHIST
00250         UNTIL WS-DONE-PRINTING.                                   TIMEHIST
00251 *                                                                 TIMEHIST
00252 *                                                                 TIMEHIST
00253  300-FP-EXIT.                                                     TIMEHIST
00254      EXIT.                                                        TIMEHIST
00255 *                                                                 TIMEHIST
00256 *                                                                 TIMEHIST
00257  310-INPUT-DATA.                                                  TIMEHIST
00258      READ UDAI-DATA AT END                                        TIMEHIST
00259         GO TO 310-ID-EXIT.                                        TIMEHIST
00260      ADD 1 TO WS-TOTAL.                                              CL**4
00261      IF UDAI-ADDR IS LESS THAN WS-START                           TIMEHIST
00262         ADD 1 TO WS-LOW-COUNT                                     TIMEHIST
00263      ELSE                                                         TIMEHIST
00264         IF UDAI-ADDR IS IS GREATER THAN WS-HIGH                   TIMEHIST
00265            ADD 1 TO WS-HIGH-COUNT                                 TIMEHIST
00266         ELSE                                                      TIMEHIST
00267            SUBTRACT WS-START FROM UDAI-ADDR                       TIMEHIST
00268            DIVIDE WS-ILEN INTO UDAI-ADDR                          TIMEHIST
00269            ADD 1 TO UDAI-ADDR                                        CL**2
00270            MOVE LOW-VALUES TO WS-WORK-1B                          TIMEHIST
00271            MOVE WS-TIME-COUNT (UDAI-ADDR) TO WS-WORK-3B           TIMEHIST
00272            ADD 1 TO WS-WORK-COUNT                                 TIMEHIST
00273            MOVE WS-WORK-3B TO WS-TIME-COUNT (UDAI-ADDR).          TIMEHIST
00274      GO TO 310-INPUT-DATA.                                        TIMEHIST
00275 *                                                                 TIMEHIST
00276 *                                                                 TIMEHIST
00277  310-ID-EXIT.                                                     TIMEHIST
00278      EXIT.                                                        TIMEHIST
00279 *                                                                 TIMEHIST
00280 *                                                                 TIMEHIST
00281  320-MAX-GET.                                                     TIMEHIST
00282      IF WS-TIME-COUNT (WS-TC-IX) IS GREATER THAN WS-WORK-3B          CL**2
00283         MOVE WS-TIME-COUNT (WS-TC-IX) TO WS-WORK-3B.                 CL**2
00284      SET WS-TC-IX UP BY 1.                                        TIMEHIST
00285      IF WS-TC-IX IS LESS THAN 43691                               TIMEHIST
00286         GO TO 320-MAX-GET.                                        TIMEHIST
00287      MOVE WS-WORK-COUNT TO WS-MAX.                                   CL**5
00288 *                                                                 TIMEHIST
00289 *                                                                 TIMEHIST
00290  320-MG-EXIT.                                                     TIMEHIST
00291      EXIT.                                                        TIMEHIST
00292 *                                                                 TIMEHIST
00293 *                                                                 TIMEHIST
00294  330-PRINT-ALL.                                                   TIMEHIST
00295      IF WS-TC-IX IS NOT LESS THAN 43691                           TIMEHIST
00296         MOVE 'D' TO WS-PRINT-FLAG                                 TIMEHIST
00297         GO TO 330-PA-EXIT.                                        TIMEHIST
00298      SET WS-PT-IX TO 1.                                           TIMEHIST
00299      MOVE LOW-VALUES TO WS-PRINT-TABLE.                           TIMEHIST
00300      PERFORM 340-FILL-TABLE THRU 340-FT-EXIT                      TIMEHIST
00301      PERFORM 350-PRINT-TABLE THRU 350-PT-EXIT.                    TIMEHIST
00302 *                                                                 TIMEHIST
00303 *                                                                 TIMEHIST
00304  330-PA-EXIT.                                                     TIMEHIST
00305      EXIT.                                                        TIMEHIST
00306 *                                                                 TIMEHIST
00307 *                                                                 TIMEHIST
00308  340-FILL-TABLE.                                                  TIMEHIST
00309      IF WS-TC-IX IS EQUAL TO 1                                    TIMEHIST
00310         MOVE 'LESS' TO WS-FROMX (WS-PT-IX)                        TIMEHIST
00311         MOVE WS-LOW-COUNT TO WS-TIMES (WS-PT-IX)                  TIMEHIST
00312         MOVE ZERO TO WS-WORK-AST                                     CL**5
00313         MOVE WS-WORK-AST TO WS-AST-CNT (WS-PT-IX)                 TIMEHIST
00314         SET WS-PT-IX UP BY 1.                                     TIMEHIST
00315      IF WS-TC-IX IS EQUAL TO 43691                                TIMEHIST
00316         MOVE 'GTR ' TO WS-FROMX (WS-PT-IX)                        TIMEHIST
00317         MOVE WS-HIGH-COUNT TO WS-TIMES (WS-PT-IX)                 TIMEHIST
00318         MOVE ZERO TO WS-WORK-AST                                     CL**5
00319         MOVE WS-WORK-AST TO WS-AST-CNT (WS-PT-IX)                 TIMEHIST
00320         SET WS-PT-IX UP BY 1                                      TIMEHIST
00321         GO TO 340-FT-EXIT.                                        TIMEHIST
00322      IF WS-TIME-COUNT (WS-TC-IX) IS NOT EQUAL TO LOW-VALUES       TIMEHIST
00323         SET WS-WORK-COUNT TO WS-TC-IX                             TIMEHIST
00324         SUBTRACT 1 FROM WS-WORK-COUNT                                CL**3
00325         MULTIPLY WS-ILEN BY WS-WORK-COUNT                         TIMEHIST
00326         ADD WS-START TO WS-WORK-COUNT                             TIMEHIST
00327         MOVE WS-WORK-COUNT TO WS-FROM (WS-PT-IX)                  TIMEHIST
00328         MOVE WS-TIME-COUNT (WS-TC-IX) TO WS-WORK-3B               TIMEHIST
00329         MOVE LOW-VALUES TO WS-WORK-1B                             TIMEHIST
00330         MOVE WS-WORK-COUNT TO WS-TIMES (WS-PT-IX)                 TIMEHIST
00331         COMPUTE WS-WORK-AST = (WS-WORK-COUNT * 45) / WS-MAX       TIMEHIST
00332         MOVE WS-WORK-AST TO WS-AST-CNT (WS-PT-IX)                 TIMEHIST
00333         SET WS-PT-IX UP BY 1.                                     TIMEHIST
00334      SET WS-TC-IX UP BY 1.                                        TIMEHIST
00335      IF WS-PT-IX IS LESS THAN 101                                 TIMEHIST
00336         AND WS-TC-IX IS NOT GREATER THAN 43691                    TIMEHIST
00337         GO TO 340-FILL-TABLE.                                     TIMEHIST
00338 *                                                                 TIMEHIST
00339 *                                                                 TIMEHIST
00340  340-FT-EXIT.                                                     TIMEHIST
00341      EXIT.                                                        TIMEHIST
00342 *                                                                 TIMEHIST
00343 *                                                                 TIMEHIST
00344 *                                                                 TIMEHIST
00345  350-PRINT-TABLE.                                                 TIMEHIST
00346      MOVE WS-TITLE TO UPRO-LINE.                                  TIMEHIST
00347      WRITE UPRO-REC AFTER ADVANCING TOP-OF-PAGE.                  TIMEHIST
00348      MOVE WS-CAPT TO UPRO-LINE.                                   TIMEHIST
00349      WRITE UPRO-REC AFTER ADVANCING 3 LINES.                      TIMEHIST
00350      SET WS-PT-IX TO 1.                                           TIMEHIST
00351 *                                                                 TIMEHIST
00352  350-LOOP.                                                        TIMEHIST
00353      PERFORM 360-BUILD-HALF THRU 360-BH-EXIT.                     TIMEHIST
00354      MOVE WS-HALF TO WS-PL-1ST-HALF.                              TIMEHIST
00355      SET WS-PT-IX UP BY 50.                                       TIMEHIST
00356      PERFORM 360-BUILD-HALF THRU 360-BH-EXIT.                     TIMEHIST
00357      MOVE WS-HALF TO WS-PL-2ND-HALF.                              TIMEHIST
00358      MOVE WS-PRINT-LINE TO UPRO-LINE.                             TIMEHIST
00359      WRITE UPRO-REC AFTER ADVANCING 1 LINES.                      TIMEHIST
00360      SET WS-PT-IX DOWN BY 49.                                     TIMEHIST
00361      IF WS-PT-IX IS NOT EQUAL TO 51                               TIMEHIST
00362         GO TO 350-LOOP.                                           TIMEHIST
00363 *                                                                 TIMEHIST
00364 *                                                                 TIMEHIST
00365  350-PT-EXIT.                                                     TIMEHIST
00366      EXIT.                                                        TIMEHIST
00367 *                                                                 TIMEHIST
00368 *                                                                 TIMEHIST
00369  360-BUILD-HALF.                                                  TIMEHIST
00370      IF WS-TIMES (WS-PT-IX) IS EQUAL TO ZERO                      TIMEHIST
00371         MOVE SPACES TO WS-HALF                                    TIMEHIST
00372         GO TO 360-BH-EXIT.                                        TIMEHIST
00373      MOVE SPACES TO WS-LG-FLAG.                                   TIMEHIST
00374      IF WS-FROMX (WS-PT-IX) IS EQUAL TO 'LESS'                    TIMEHIST
00375         MOVE 'L' TO WS-LG-FLAG                                    TIMEHIST
00376         MOVE WS-START TO WS-FROM (WS-PT-IX)                       TIMEHIST
00377         ADD 1 TO WS-FROM (WS-PT-IX)                               TIMEHIST
00378         SUBTRACT WS-ILEN FROM WS-FROM (WS-PT-IX).                 TIMEHIST
00379      IF WS-FROMX (WS-PT-IX) IS EQUAL TO 'GTR '                    TIMEHIST
00380         MOVE 'G' TO WS-LG-FLAG                                    TIMEHIST
00381         MOVE WS-ILEN TO WS-WORK-COUNT                             TIMEHIST
00382         MULTIPLY 43690 BY WS-WORK-COUNT                           TIMEHIST
00383         ADD WS-START TO WS-WORK-COUNT                             TIMEHIST
00384         MOVE WS-WORK-COUNT TO WS-FROM (WS-PT-IX).                 TIMEHIST
00385      MOVE '      -' TO WS-HALF.                                   TIMEHIST
00386      MOVE WS-FROM (WS-PT-IX) TO WS-WORK-COUNT.                    TIMEHIST
00387      PERFORM 370-CVT-TO-HEX THRU 370-CTH-EXIT.                    TIMEHIST
00388      MOVE WS-HEX-ADDR-F TO WS-HALF-FROM.                          TIMEHIST
00389      MOVE WS-FROM (WS-PT-IX) TO WS-WORK-COUNT.                    TIMEHIST
00390      ADD WS-ILEN TO WS-WORK-COUNT.                                TIMEHIST
00391      SUBTRACT 1 FROM WS-WORK-COUNT.                               TIMEHIST
00392      PERFORM 370-CVT-TO-HEX THRU 370-CTH-EXIT.                    TIMEHIST
00393      MOVE WS-HEX-ADDR-F TO WS-HALF-TO.                            TIMEHIST
00394      MOVE WS-TIMES (WS-PT-IX) TO WS-HALF-TIMES.                   TIMEHIST
00395      IF WS-LESS                                                   TIMEHIST
00396         MOVE 'LESS' TO WS-HALF-FROM.                              TIMEHIST
00397      IF WS-GTR                                                    TIMEHIST
00398         MOVE 'GTR' TO WS-HALF-TO.                                 TIMEHIST
00399      SET WS-HA-IX TO 1.                                           TIMEHIST
00400  360-LOOP.                                                        TIMEHIST
00401      IF WS-HA-IX IS NOT GREATER THAN WS-AST-CNT (WS-PT-IX)        TIMEHIST
00402         MOVE '*' TO WS-HA-CH (WS-HA-IX)                           TIMEHIST
00403         SET WS-HA-IX UP BY 1                                      TIMEHIST
00404         GO TO 360-LOOP.                                           TIMEHIST
00405 *                                                                 TIMEHIST
00406 *                                                                 TIMEHIST
00407  360-BH-EXIT.                                                     TIMEHIST
00408      EXIT.                                                        TIMEHIST
00409 *                                                                 TIMEHIST
00410 *                                                                 TIMEHIST
00411  370-CVT-TO-HEX.                                                  TIMEHIST
00412      SET WS-HAC-IX TO 6.                                          TIMEHIST
00413  370-LOOP.                                                        TIMEHIST
00414      DIVIDE 16 INTO WS-WORK-COUNT GIVING WS-WORK-COUNT            TIMEHIST
00415         REMAINDER WS-REM.                                         TIMEHIST
00416      ADD 1 TO WS-REM.                                             TIMEHIST
00417      MOVE WS-HEX-CHAR (WS-REM) TO WS-HEX-ADDR-CH (WS-HAC-IX).     TIMEHIST
00418      IF WS-HAC-IX IS NOT EQUAL TO 1                               TIMEHIST
00419         SET WS-HAC-IX DOWN BY 1                                   TIMEHIST
00420         GO TO 370-LOOP.                                           TIMEHIST
00421 *                                                                 TIMEHIST
00422 *                                                                 TIMEHIST
00423  370-CTH-EXIT.                                                    TIMEHIST
00424      EXIT.                                                        TIMEHIST
00425 *                                                                 TIMEHIST
00426 *                                                                 TIMEHIST
